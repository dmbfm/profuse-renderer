:: Build script for "quick-console"

@echo off

SETLOCAL

:: Pre-defined configurations (Optional)
rem SET CONFIGURATION=RELEASE
rem SET CONFIGURATION=DEBUG

SET EXECUTABLE_NAME=main.exe

if "%CONFIGURATION%"=="RELEASE" (
	SET DEBUG=FALSE
	SET SPEED=FAST
	GOTO COMPILER
) ELSE IF "%CONFIGURATION%"=="DEBUG" (
	SET DEBUG=TRUE
	SET SPEED=SLOW
	GOTO COMPILER
)


:: Custom configuration

:: Debuging
SET DEBUG=TRUE
rem SET DEBUG=FALSE

:: Optimizations
 SET SPEED=SLOW
rem SET SPEED=MEDIUM
rem SET SPEED=FAST


:COMPILER
SET COMPILER=MSVC
rem SET COMPILER=CLANG
rem SET COMPILER=CLANGCL

IF "%COMPILER%"=="MSVC" (
	GOTO MSVC
) ELSE (
	IF "%COMPILER%"=="CLANGCL" (
		GOTO MSVC
	) ELSE (
		GOTO CLANG
	)
)

:: MSVC START
:MSVC
ECHO %~dp0
SET BASE_DIR=%~dp0
SET SOURCE_DIR=%BASE_DIR%src
SET OUTPUT_DIR=.\build
SET OUTPUT=%EXECUTABLE_NAME%
SET INPUT=%SOURCE_DIR%\main.c
SET CC=cl
SET CFLAGS=/Wall /wd4204 /wd4711 /wd5045 /wd4221 /wd4668 /wd4255 /wd4820 /wd4100 /wd4710 /wd4706 /wd4201 /fp:fast /GS- /Zo -D_CRT_SECURE_NO_DEPRECATE  user32.lib gdi32.lib

:: clang-cl is compatible with cl.exe 
IF "%COMPILER%"=="CLANGCL" (
	SET CC=clang-cl
)

IF "%DEBUG%"=="TRUE" (
	SET CFLAGS=%CFLAGS%  /Z7 -DTG_DEBUG
) ELSE (
	SET CFLAGS=%CFLAGS%
)

IF "%SPEED%"=="SLOW" (
	SET CFLAGS=%CFLAGS% /Od -DTG_SPEED_SLOW
) ELSE IF %SPEED%=="MEDIUM" (
	SET CFLAGS=%CFLAGS% /O1 -DTG_SPEED_MEDIUM
) ELSE (
	SET CFLAGS=%CFLAGS% /O2 -DTG_SPEED_FAST
)

if not exist %OUTPUT_DIR% mkdir %OUTPUT_DIR%

pushd %OUTPUT_DIR%

@echo on
%CC% %INPUT% %CFLAGS% /link /out:%OUTPUT%
@echo off

popd
GOTO END
:: MSVC END

:: CLANG START
:CLANG
SET SOURCE_DIR=.\src
SET OUTPUT_DIR=.\build
SET OUTPUT=-o %OUTPUT_DIR%\%EXECUTABLE_NAME%
SET INPUT=%SOURCE_DIR%\main.c
SET CC=clang
SET CFLAGS=-std=c99 -Weverything

IF "%DEBUG%"=="TRUE" (
	SET CFLAGS=%CFLAGS%  -g -DTG_DEBUG
) ELSE (
	SET CFLAGS=%CFLAGS% -DNDEBUG
)

IF "%SPEED%"=="SLOW" (
	SET CFLAGS=%CFLAGS% -O0 -DTG_SPEED_SLOW
) ELSE IF %SPEED%=="MEDIUM" (
	SET CFLAGS=%CFLAGS% -O1 -DTG_SPEED_MEDIUM
) ELSE (
	SET CFLAGS=%CFLAGS% -O2 -DTG_SPEED_FAST
)
@echo on
%CC% %INPUT% %CFLAGS% %OUTPUT%
:: CLANG END
:END
